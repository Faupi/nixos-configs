# Adds options for setting LocalStorage defaults for Chromium
{ config, pkgs, lib, ... }:
let
  inherit (builtins) map attrNames toJSON;
  inherit (lib) mkOption types mkIf mkAfter;
  inherit (pkgs) writeText runCommand;
  cfg = config.programs.chromium.localStorageDefaults;

  matches = map (u: "${u}/*") (attrNames cfg);

  manifestJSON = writeText "manifest.json" (toJSON {
    manifest_version = 3;
    name = "Site LocalStorage Defaults";
    version = "1.0.0";
    description = "Sets localStorage defaults from a JSON file generated by Nix.";

    content_scripts = [
      {
        inherit matches;
        js = [ "inject.js" ];
        run_at = "document_start";
      }
    ];
    web_accessible_resources = [
      {
        inherit matches;
        resources = [ "config.json" ];
      }
    ];
  });

  injectJS = ./extension/inject.js;

  configJSON = writeText "config.json"
    (toJSON cfg);

  extension = runCommand "localstorage-extension" { } ''
    mkdir -p $out
    cp ${manifestJSON} $out/manifest.json
    cp ${injectJS} $out/inject.js
    cp ${configJSON} $out/config.json
  '';
in
{
  options = {
    programs.chromium.localStorageDefaults = mkOption {
      type = types.attrsOf (types.attrsOf types.anything);
      default = { };
      example = { "https://s.dunkirk.sh" = { "mykey" = "myvalue"; }; };
      description = "Per-origin localStorage defaults";
    };
  };

  config = mkIf (cfg != { }) {
    programs.chromium.commandLineArgs = mkAfter [
      "--load-extension=${extension}"
    ];
  };
}
