# Adds options for setting LocalStorage defaults for Chromium
{ config, pkgs, lib, ... }:
let
  inherit (builtins) map attrNames toJSON;
  inherit (lib) mkOption types mkIf mkAfter;
  inherit (pkgs) writeText runCommand;
  cfg = config.programs.vivaldi.localStorageDefaults;

  matches = map (u: "${u}/*") (attrNames cfg.origins);
  # To refresh the key and extension ID, manually pack it and check via https://robwu.nl/crxviewer/
  key = ''MIIBIjANBgkqhkiG9w0BAQEFAAOCAQ8AMIIBCgKCAQEA5ugZ5EYiKQUc0oBS3UkMYypuwTBRVN0JjLZn1ksPRaznil15iwmD4vu8QpLy/qolKfc5vIl8+TZ1z1jSxta7KmZw8nAPioRjtFgC2rv/uurlVG3VVUoMfXCJ0uBDvZG8/433w9W3Dl2hOKF2PLLk6cdZObeD8nM8JhZ4VqmysYcNmp8rezEdeXeJPD4JUWZq01WaWzvZ/btih3EM1EHlReDZrqzxjUdFuYRWuINW9QMQEd3W5LsvbIOWKJqnd2IFh0QFPFSVKSe0DOFUjyO11eQmHXGUJ+SkmAmFry4YWoB0qtJ+9n/pNzdjozTSkdZFifSAm2sO6ctHIxUGLJAhdwIDAQAB'';
  extensionId = ''ihjlkgbbkkoihjdcfcjhbnipocfngggf'';

  manifestJSON = writeText "manifest.json" (toJSON {
    manifest_version = 3;
    name = "Site LocalStorage Defaults";
    version = "1.0.0";
    description = "Sets localStorage defaults from a JSON file generated by Nix.";
    inherit key;

    content_scripts = [
      {
        inherit matches;
        js = [ "inject.js" ];
        run_at = "document_start";
      }
    ];
    web_accessible_resources = [
      {
        inherit matches;
        resources = [ "config.json" ];
      }
    ];
  });

  injectJS = ./extension/inject.js;

  configJSON = writeText "config.json"
    (toJSON cfg.origins);

  extension = runCommand "localstorage-extension" { } ''
    mkdir -p $out
    cp ${manifestJSON} $out/manifest.json
    cp ${injectJS} $out/inject.js
    cp ${configJSON} $out/config.json
  '';
in
{
  options = {
    programs.vivaldi.localStorageDefaults = {
      origins = mkOption {
        description = "Per-origin localStorage defaults";
        type = types.attrsOf (types.attrsOf types.anything);
        default = { };
        example = { "https://s.dunkirk.sh" = { "mykey" = "myvalue"; }; };
      };
      extensionId = mkOption {
        description = "ID of the extension used for readback for customization purposes";
        type = types.str;
        default = extensionId;
        readOnly = true;
      };
    };
  };

  config = mkIf (cfg.origins != { }) {
    programs.vivaldi.commandLineArgs = mkAfter [
      "--load-extension=${extension}"
    ];
  };
}
